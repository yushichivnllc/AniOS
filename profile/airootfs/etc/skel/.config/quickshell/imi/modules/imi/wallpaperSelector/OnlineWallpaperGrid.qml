import qs.modules.common
import qs.modules.common.widgets
import qs.modules.common.functions
import qs.services
import QtQuick
import QtQuick.Layouts
import Qt5Compat.GraphicalEffects
import Quickshell
import Quickshell.Io

Item {
    id: root

    property string provider: "wallhaven"
    property string resolution: "1080p"
    property int columns: Config.options.wallpaperSelector.columns || 4
    property real previewCellAspectRatio: 4 / 3
    property var hoveredItem: null

    signal wallpaperSelected(string path)
    signal updateThumbnailsRequested()

    readonly property bool unsplashMissingKey:
        root.provider === "unsplash" &&
        (KeyringStorage.keyringData?.apiKeys?.unsplash ?? "").length === 0

    readonly property bool pexelsMissingKey:
        root.provider === "pexels" &&
        (KeyringStorage.keyringData?.apiKeys?.pexels ?? "").length === 0
        
    readonly property bool missingKey: root.unsplashMissingKey || root.pexelsMissingKey

    onProviderChanged:   { root.hoveredItem = null; _syncAndFetch() }
    onResolutionChanged: _syncAndFetch()

    function _syncAndFetch() {
        if (root.missingKey) return
        OnlineWallpapers.provider   = root.provider
        OnlineWallpapers.resolution = root.resolution
        OnlineWallpapers.fetch()
    }

    function moveSelection(delta) {
        grid.currentIndex = Math.max(0, Math.min(wallpaperModel.count - 1, grid.currentIndex + delta))
        grid.positionViewAtIndex(grid.currentIndex, GridView.Contain)
    }

    function activateCurrent() {
        const item = wallpaperModel.get(grid.currentIndex)
        if (!item) return
        const url = item.full
        const urlLower = url.toLowerCase().split("?")[0]
        const ext = urlLower.includes(".png") ? "png"
            : urlLower.includes(".webp") ? "webp"
            : urlLower.includes(".jpeg") ? "jpg"
            : "jpg"
        const fileName = `${item.provider}-${item.id}.${ext}`
        const picturesPath = Directories.pictures.toString().replace("file://", "")
        const fullPath = `${picturesPath}/Wallpapers/${fileName}`
        downloadProc.filePath = fullPath
        downloadProc.applyAfter = true
        // Positional args ($1-$3), never spliced into the script body: item.full
        // is a URL from remote provider JSON (Wallhaven/Unsplash/Pexels), so
        // interpolating it into the shell string was injectable.
        downloadProc.command = ["bash", "-c",
            'mkdir -p "$1/Wallpapers" && curl -L --silent "$2" -o "$3"',
            "bash", picturesPath, item.full, fullPath
        ]
        downloadProc.running = true
    }

    Component.onCompleted: _syncAndFetch()

    ListModel { id: wallpaperModel }

    Connections {
        target: OnlineWallpapers
        function onFetched() {
            if (!OnlineWallpapers.appending) {
                wallpaperModel.clear()
                root.hoveredItem = null
            }
            const startIndex = wallpaperModel.count
            for (const item of OnlineWallpapers.results.slice(startIndex)) {
                wallpaperModel.append(item)
            }
        }
        function onFetchError(message) {
            console.log("[OnlineWallpaperGrid] Error:", message)
        }
    }

    Process {
        id: downloadProc
        property string filePath: ""
        property bool applyAfter: false

        stdout: SplitParser {
            onRead: data => console.log("[download]", data)
        }

        onExited: (exitCode) => {
            if (exitCode === 0) {
                if (applyAfter) root.wallpaperSelected(filePath)
                Wallpapers.setDirectory(Wallpapers.effectiveDirectory)
                Qt.callLater(() => root.updateThumbnailsRequested())
                Quickshell.execDetached(["notify-send",
                    applyAfter ? Translation.tr("Wallpaper applied") : Translation.tr("Download complete"),
                    filePath, "-a", "Shell"
                ])
            } else {
                Quickshell.execDetached(["notify-send",
                    Translation.tr("Download failed"), filePath, "-a", "Shell"
                ])
            }
        }
    }

    // Missing key. The two lines under the title - the command to run and
    // where the key comes from - are one description here: the placeholder
    // carries a single body, and they were always read as one instruction.
    PagePlaceholder {
        shown: root.missingKey
        icon: "key_off"
        shape: MaterialShape.Shape.Cookie7Sided
        title: root.unsplashMissingKey
            ? Translation.tr("Unsplash API key not set")
            : Translation.tr("Pexels API key not set")
        description: root.unsplashMissingKey
            ? Translation.tr("Open the launcher and run:\n/unsplash YOUR_API_KEY\n\nGet your free key at unsplash.com/developers")
            : Translation.tr("Open the launcher and run:\n/pexels YOUR_API_KEY\n\nGet your free key at pexels.com/api")
        descriptionHorizontalAlignment: Text.AlignHCenter
    }

    // Loading
    StyledIndeterminateProgressBar {
        visible: OnlineWallpapers.loading
        anchors {
            top: parent.top
            left: parent.left
            right: parent.right
            leftMargin: Appearance.spacing.space50
            rightMargin: Appearance.spacing.space50
        }
    }

    // Grid online
    Item {
        id: gridContainer
        anchors.fill: parent
        visible: !root.missingKey

        GridView {
            id: grid
            anchors {
                top: parent.top
                left: parent.left
                right: parent.right
                bottom: parent.bottom
            }
            visible: wallpaperModel.count > 0

            property int currentIndex: 0

            cellWidth: width / root.columns
            cellHeight: cellWidth / root.previewCellAspectRatio
            interactive: true
            clip: true
            boundsBehavior: Flickable.StopAtBounds

            model: wallpaperModel

            delegate: Item {
                id: delegateItem
                required property var model
                required property int index

                width: grid.cellWidth
                height: grid.cellHeight

                Image {
                    id: thumb
                    anchors.fill: parent
                    anchors.margins: Appearance.sizes.wallpaperSelectorItemMargins
                    source: delegateItem.model.thumb
                    fillMode: Image.PreserveAspectCrop
                    asynchronous: true
                    cache: true

                    layer.enabled: true
                    layer.effect: OpacityMask {
                        maskSource: Rectangle {
                            width: thumb.width
                            height: thumb.height
                            radius: Appearance.rounding.normal
                        }
                    }

                    Rectangle {
                        anchors.fill: parent
                        radius: Appearance.rounding.normal
                        color: delegateItem.index === grid.currentIndex
                            ? Qt.rgba(
                                Appearance.colors.colPrimary.r,
                                Appearance.colors.colPrimary.g,
                                Appearance.colors.colPrimary.b, 0.35)
                            : "transparent"
                        border.width: delegateItem.index === grid.currentIndex ? 2 : 0
                        border.color: Appearance.colors.colPrimary
                    }

                    Rectangle {
                        anchors.fill: parent
                        radius: Appearance.rounding.normal
                        color: Appearance.colors.colLayer2
                        visible: thumb.status !== Image.Ready
                        MaterialSymbol {
                            anchors.centerIn: parent
                            text: "image"
                            iconSize: 32
                            color: Appearance.colors.colSubtext
                        }
                    }
                }

                MouseArea {
                    anchors.fill: parent
                    hoverEnabled: true
                    acceptedButtons: Qt.LeftButton | Qt.RightButton
                    onEntered: {
                        grid.currentIndex = delegateItem.index
                        root.hoveredItem = delegateItem.model
                        root.forceActiveFocus()
                    }
                    onClicked: event => {
                        const url = delegateItem.model.full
                        const urlLower = url.toLowerCase().split("?")[0]
                        const ext = urlLower.includes(".png") ? "png"
                            : urlLower.includes(".webp") ? "webp"
                            : urlLower.includes(".jpeg") ? "jpg"
                            : "jpg"
                        const fileName = `${delegateItem.model.provider}-${delegateItem.model.id}.${ext}`
                        const picturesPath = Directories.pictures.toString().replace("file://", "")
                        const fullPath = `${picturesPath}/Wallpapers/${fileName}`
                        downloadProc.filePath = fullPath
                        downloadProc.applyAfter = event.button === Qt.LeftButton
                        // Positional args ($1-$3): delegateItem.model.full is a
                        // remote provider URL, unsafe to splice into the shell body.
                        downloadProc.command = ["bash", "-c",
                            'mkdir -p "$1/Wallpapers" && curl -L --silent "$2" -o "$3"',
                            "bash", picturesPath, delegateItem.model.full, fullPath
                        ]
                        downloadProc.running = true
                    }
                }
            }

            layer.enabled: true
            layer.effect: OpacityMask {
                maskSource: Rectangle {
                    width: grid.width
                    height: grid.height
                    radius: Appearance.rounding.screenRounding + 5
                }
            }
            onContentYChanged: {
                if (!OnlineWallpapers.loading
                    && contentY + height >= contentHeight - cellHeight * 1.5) {
                    OnlineWallpapers.nextPage()
                }
            }
        }

        // Empty state. PagePlaceholder carries no action of its own, so the
        // placeholder takes a block of its own in this column and the retry
        // button stays underneath it.
        ColumnLayout {
            anchors.centerIn: parent
            visible: wallpaperModel.count === 0 && !OnlineWallpapers.loading
            spacing: Appearance.spacing.space150

            Item {
                Layout.alignment: Qt.AlignHCenter
                implicitWidth: 320
                implicitHeight: 180

                PagePlaceholder {
                    icon: "cloud_off"
                    shape: MaterialShape.Shape.Cookie7Sided
                    title: Translation.tr("No results")
                    description: Translation.tr("Try fetching again")
                    descriptionHorizontalAlignment: Text.AlignHCenter
                }
            }

            RippleButton {
                Layout.alignment: Qt.AlignHCenter
                implicitHeight: 36
                buttonRadius: height / 2
                colBackground: Appearance.colors.colSecondaryContainer
                onClicked: OnlineWallpapers.fetch()
                contentItem: RowLayout {
                    anchors.centerIn: parent
                    spacing: Appearance.spacing.space100
                    MaterialSymbol {
                        text: "refresh"
                        iconSize: Appearance.font.pixelSize.larger
                        color: Appearance.colors.colOnSecondaryContainer
                    }
                    StyledText {
                        text: Translation.tr("Retry")
                        color: Appearance.colors.colOnSecondaryContainer
                    }
                }
            }
        }
    }
}
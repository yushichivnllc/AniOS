pragma ComponentBehavior: Bound
import qs.modules.common
import qs.modules.common.widgets
import qs.modules.imi.mediaControls
import qs.modules.common.functions
import qs.services
import qs.modules.common.models
import qs
import Qt5Compat.GraphicalEffects
import QtQuick
import QtQuick.Effects
import QtQuick.Layouts
import Quickshell
import Quickshell.Io
import Quickshell.Services.Mpris
import "../../common/functions/media_art.js" as MediaArt

Item {
    id: root
    
    property bool vertical: false
    property bool isMaterial: Config.options.bar.cornerStyle === 3
    readonly property MprisPlayer activePlayer: MprisController.activePlayer

    readonly property string cleanedTitle: StringUtils.cleanMusicTitle(activePlayer?.trackTitle) || Translation.tr("No media")

    property var    artUrl:      MediaArt.resolve(activePlayer?.trackArtUrl ?? "", activePlayer?.metadata)
    property string trackTitle:  activePlayer?.trackTitle  ?? ""
    property string trackArtist: activePlayer?.trackArtist ?? ""
    property bool   isPlaying:   activePlayer?.isPlaying   ?? false
    property bool   hasTrack:    trackTitle.length > 0

    MediaArtSource {
        id: artSource
        artUrl: root.artUrl
    }
    readonly property string displayedArtFilePath: artSource.displayedArtFilePath
    readonly property bool artDownloaded: artSource.downloaded

    Layout.fillHeight: true
    implicitWidth: vertical 
        ? Appearance.sizes.verticalBarWidth 
        : (isMaterial 
            ? materialRow.implicitWidth 
            : Math.max(
                Config.options.bar.media.minWidth,
                Math.min(rowLayout.implicitWidth + 8, Config.options.bar.media.maxWidth)
            ))
    implicitHeight: vertical ? (isMaterial ? 32 : mediaCircProg.implicitHeight) : Appearance.sizes.barHeight

    Timer {
        running: activePlayer?.playbackState == MprisPlaybackState.Playing
        interval: Config.options.resources.updateInterval
        repeat: true
        onTriggered: activePlayer.positionChanged()
    }

    MouseArea {
        anchors.fill: parent
        cursorShape: Qt.PointingHandCursor
        acceptedButtons: Qt.MiddleButton | Qt.BackButton | Qt.ForwardButton | Qt.RightButton | Qt.LeftButton
        hoverEnabled: !Config.options.bar.tooltips.clickToShow
        onPressed: (event) => {
            if (event.button === Qt.MiddleButton)      activePlayer?.togglePlaying()
            else if (event.button === Qt.BackButton)   activePlayer?.previous()
            else if (event.button === Qt.ForwardButton || event.button === Qt.RightButton) activePlayer?.next()
            else if (event.button === Qt.LeftButton)   GlobalStates.mediaControlsOpen = !GlobalStates.mediaControlsOpen
        }
    }

    // Vertical default. The progress circle is the outline ring - an icon in
    // a circle is outlined under every bar style but M3 (BarIconRing says
    // why); the filled ring stays the resource monitor's own option.
    Loader {
        id: mediaCircProg
        active: root.vertical && !root.isMaterial
        visible: active
        anchors.centerIn: parent
        sourceComponent: ClippedOutlineCircularProgress {
            implicitSize: 20
            lineWidth: Appearance.rounding.unsharpen
            value: (root.activePlayer?.length ?? 0) > 0 ? root.activePlayer.position / root.activePlayer.length : 0
            colPrimary: Appearance.colors.colOnSecondaryContainer
            enableAnimation: false
            Item {
                anchors.centerIn: parent
                width: 20
                height: 20
                MaterialSymbol {
                    anchors.centerIn: parent
                    fill: 1
                    text: root.activePlayer?.isPlaying ? "pause" : "music_note"
                    iconSize: Appearance.font.pixelSize.normal
                    color: Appearance.colors.colOnSecondaryContainer
                }
            }
        }
    }

    // Vertical Material
    Rectangle {
        visible: root.vertical && root.isMaterial
        anchors.centerIn: parent
        color: Appearance.colors.colSecondaryContainer
        radius: Appearance.rounding.full
        implicitWidth: 32
        implicitHeight: 32
        
        MaterialSymbol {
            anchors.centerIn: parent
            fill: 1
            text: root.activePlayer?.isPlaying ? "pause" : "music_note"
            iconSize: Appearance.font.pixelSize.normal
            color: Appearance.colors.colOnSecondaryContainer
        }
    }

    // Horizontal default
    Loader {
        id: rowLayout
        active: !root.vertical && !root.isMaterial
        visible: active
        anchors.fill: parent
        sourceComponent: RowLayout {
            spacing: Appearance.spacing.space50
            ClippedOutlineCircularProgress {
                Layout.alignment: Qt.AlignVCenter
                Layout.leftMargin: Appearance.spacing.space50
                implicitSize: 20
                lineWidth: Appearance.rounding.unsharpen
                value: (root.activePlayer?.length ?? 0) > 0 ? root.activePlayer.position / root.activePlayer.length : 0
                colPrimary: Appearance.colors.colOnSecondaryContainer
                enableAnimation: false
                Item {
                    anchors.centerIn: parent
                    width: 20
                    height: 20
                    MaterialSymbol {
                        anchors.centerIn: parent
                        fill: 1
                        text: root.activePlayer?.isPlaying ? "pause" : "music_note"
                        iconSize: Appearance.font.pixelSize.normal
                        color: Appearance.colors.colOnSecondaryContainer
                    }
                }
            }
            StyledText {
                visible: Config.options.bar.verbose
                Layout.alignment: Qt.AlignVCenter
                Layout.fillWidth: true
                Layout.rightMargin: 0
                horizontalAlignment: Text.AlignHCenter
                elide: Text.ElideRight
                color: Appearance.colors.colOnLayer1
                // A track change is an event the eye follows, not a tick - the
                // material row's two labels already animate their swaps.
                animateChange: true
                text: Config.options.bar.media.onlyTitle ? root.cleanedTitle : `${root.cleanedTitle}${root.activePlayer?.trackArtist ? ' • ' + root.activePlayer.trackArtist : ''}`
            }
        }
    }

    // Horizontal Material
    Loader {
        id: materialRow
        active: !root.vertical && root.isMaterial
        visible: active
        anchors.centerIn: parent
        sourceComponent: RowLayout {
            id: innerRow
            anchors.centerIn: parent
            spacing: Appearance.spacing.space100

            // No platyer 
            Loader {
                active: !root.hasTrack
                visible: active
                Layout.alignment: Qt.AlignVCenter
                sourceComponent: RowLayout {
                    spacing: Appearance.spacing.space100

                    // Avatar
                    Rectangle {
                        id: avatarRect
                        implicitWidth: 26
                        implicitHeight: 26
                        radius: Appearance.rounding.full
                        color: Appearance.colors.colPrimaryContainer
                        Layout.alignment: Qt.AlignVCenter

                        layer.enabled: true
                        layer.effect: OpacityMask {
                            maskSource: Rectangle {
                                width: avatarRect.width
                                height: avatarRect.height
                                radius: avatarRect.radius
                            }
                        }

                        Image {
                            id: avatarImage
                            anchors.fill: parent
                            source: UserAvatar.url
                            sourceSize.width: avatarRect.width * 2
                            sourceSize.height: avatarRect.height * 2
                            fillMode: Image.PreserveAspectCrop
                            onStatusChanged: {
                                if (status === Image.Error)
                                    visible = false
                            }
                        }

                        MaterialSymbol {
                            anchors.centerIn: parent
                            text: "account_circle"
                            iconSize: Appearance.font.pixelSize.normal
                            color: Appearance.colors.colOnPrimaryContainer
                            visible: avatarImage.status === Image.Error || avatarImage.status === Image.Null
                        }
                    }

                    // Centred in the pill with no top margin: the 2px it carried
                    // sat the two lines low, more air above than below.
                    ColumnLayout {
                        spacing: -Appearance.spacing.space50
                        Layout.alignment: Qt.AlignVCenter

                        StyledText {
                            text: Config.options.profile.displayName === "" ? SystemInfo.username : Config.options.profile.displayName
                            font.pixelSize: Appearance.font.pixelSize.smaller
                            color: Appearance.colors.colOnSecondaryContainer
                            elide: Text.ElideRight
                            Layout.maximumWidth: 120
                        }

                        StyledText {
                            id: distroLabel
                            font.pixelSize: Appearance.font.pixelSize.smaller
                            color: Appearance.colors.colOnSecondaryContainer
                            opacity: 0.7
                            elide: Text.ElideRight
                            Layout.rightMargin: Appearance.spacing.space100
                            Layout.maximumWidth: 120
                            text: SystemInfo.distroName
                        }
                    }
                }
            }

            // Player
            Loader {
                active: root.hasTrack
                visible: active
                Layout.alignment: Qt.AlignVCenter
                sourceComponent: RowLayout {
                    spacing: Appearance.spacing.space100

                    // Art
                    Rectangle {
                        id: artRect
                        implicitWidth: 26
                        implicitHeight: 26
                        radius: Appearance.rounding.full
                        color: Appearance.colors.colSecondaryContainer
                        Layout.alignment: Qt.AlignVCenter

                        layer.enabled: true
                        layer.effect: OpacityMask {
                            maskSource: Rectangle {
                                width: artRect.width
                                height: artRect.height
                                radius: artRect.radius
                            }
                        }

                        StyledImage {
                            anchors.fill: parent
                            source: root.displayedArtFilePath
                            fillMode: Image.PreserveAspectCrop
                            cache: false
                            antialiasing: true
                            sourceSize.width: artRect.width
                            sourceSize.height: artRect.height
                            visible: root.displayedArtFilePath !== ""
                        }

                        MaterialSymbol {
                            anchors.centerIn: parent
                            fill: 1
                            text: "music_note"
                            iconSize: Appearance.font.pixelSize.normal
                            color: Appearance.colors.colOnSecondaryContainer
                            visible: root.displayedArtFilePath === ""
                        }
                    }

                    // Title + Artist
                    ColumnLayout {
                        spacing: -Appearance.spacing.space50
                        Layout.alignment: Qt.AlignVCenter
                        Layout.topMargin: Appearance.spacing.space25

                        StyledText {
                            id: artistText
                            text: root.trackArtist
                            font.pixelSize: Appearance.font.pixelSize.smaller
                            color: Appearance.colors.colOnSecondaryContainer
                            elide: Text.ElideRight
                            // Anchor to the art side so RTL scripts (Arabic) don't
                            // auto-align right and detach from the artwork.
                            horizontalAlignment: Text.AlignLeft
                            // Fix the slot height and center the natural line box
                            // in it: Arabic's tall font metrics then overflow
                            // symmetrically (into empty metric space) instead of
                            // growing the layout and clipping at the pill edges.
                            Layout.preferredHeight: font.pixelSize * 1.3
                            verticalAlignment: Text.AlignVCenter
                            Layout.maximumWidth: 120
                            Behavior on text {
                                SequentialAnimation {
                                    NumberAnimation { target: artistText; property: "x"; to: -artistText.width; duration: Appearance.animation.elementMoveFaster.duration; easing.type: Easing.InQuad }
                                    PropertyAction { target: artistText; property: "text" }
                                    NumberAnimation { target: artistText; property: "x"; from: artistText.width; to: 0; duration: Appearance.animation.elementMoveFaster.duration; easing.type: Easing.OutQuad }
                                }
                            }
                        }
                        StyledText {
                            id: titleText
                            Layout.topMargin: (!root.activePlayer || root.trackArtist.length === 0) ? -Appearance.spacing.space175 : 0
                            text: StringUtils.cleanMusicTitle(root.trackTitle) || Translation.tr("No media")
                            font.pixelSize: Appearance.font.pixelSize.smallie
                            color: Appearance.colors.colOnSecondaryContainer
                            elide: Text.ElideRight
                            horizontalAlignment: Text.AlignLeft
                            Layout.preferredHeight: font.pixelSize * 1.3
                            verticalAlignment: Text.AlignVCenter
                            opacity: 0.7
                            Layout.maximumWidth: 120
                            Behavior on text {
                                SequentialAnimation {
                                    NumberAnimation { target: titleText; property: "x"; to: -titleText.width; duration: Appearance.animation.elementMoveFaster.duration; easing.type: Easing.InQuad }
                                    PropertyAction { target: titleText; property: "text" }
                                    NumberAnimation { target: titleText; property: "x"; from: titleText.width; to: 0; duration: Appearance.animation.elementMoveFaster.duration; easing.type: Easing.OutQuad }
                                }
                            }
                        }
                    }

                    // Play/Pause
                    RippleButton {
                        implicitWidth: 40
                        implicitHeight: 23
                        buttonRadius: root.isPlaying ? Appearance.rounding.normal : 13
                        colBackground: root.isPlaying ? Appearance.colors.colPrimary : Appearance.colors.colSurfaceContainerLow
                        colBackgroundHover: root.isPlaying ? Appearance.colors.colPrimaryHover : Appearance.colors.colPrimaryContainerHover
                        colRipple: root.isPlaying ? Appearance.colors.colPrimaryActive : Appearance.colors.colPrimaryContainerActive
                        downAction: () => root.activePlayer?.togglePlaying()
                        contentItem: MaterialSymbol {
                            verticalAlignment: Text.AlignVCenter
                            anchors.centerIn: parent
                            horizontalAlignment: Text.AlignHCenter
                            text: root.isPlaying ? "pause" : "play_arrow"
                            iconSize: Appearance.font.pixelSize.large
                            fill: 1
                            color: root.isPlaying ? Appearance.colors.colOnPrimary : Appearance.colors.colOnPrimaryContainer
                        }
                    }

                    // Next
                    RippleButton {
                        implicitWidth: 26
                        implicitHeight: 26
                        Layout.leftMargin: -Appearance.spacing.space50
                        buttonRadius: 13
                        colBackground: "transparent"
                        colBackgroundHover: Appearance.colors.colPrimaryContainerHover
                        colRipple: Appearance.colors.colPrimaryContainerActive
                        downAction: () => root.activePlayer?.next()
                        altAction: () => root.activePlayer?.previous()
                        contentItem: MaterialSymbol {
                            verticalAlignment: Text.AlignVCenter
                            anchors.centerIn: parent
                            horizontalAlignment: Text.AlignHCenter
                            text: "skip_next"
                            iconSize: Appearance.font.pixelSize.large
                            fill: 1
                            color: Appearance.colors.colOnSecondaryContainer
                        }
                    }
                }
            }
        }
    }
}

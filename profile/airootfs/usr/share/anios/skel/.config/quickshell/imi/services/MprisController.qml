pragma Singleton
pragma ComponentBehavior: Bound

// From https://git.outfoxxed.me/outfoxxed/nixnew
// It does not have a license, but the author is okay with redistribution.

import QtQml.Models
import QtQuick
import Quickshell
import Quickshell.Io
import Quickshell.Services.Mpris
import qs.modules.common
import "MprisSelection.js" as MprisSelection
import "../modules/common/functions/media_art.js" as MediaArt

/**
 * A service that provides easy access to the active Mpris player.
 */
Singleton {
	id: root;
	property list<MprisPlayer> players: MprisSelection.candidatePlayers(
		Mpris.players.values, Config.options.media.filterDuplicatePlayers);
	property MprisPlayer trackedPlayer: null;
	property MprisPlayer activePlayer: null;
	readonly property string trackTitle: activePlayer?.trackTitle ?? "";
	readonly property string trackArtist: activePlayer?.trackArtist ?? "";
	readonly property real position: activePlayer?.position ?? 0;
	readonly property real length: activePlayer?.length ?? 0;
	signal trackChanged(reverse: bool);

	property bool __reverse: false;

	property var activeTrack;

	// The one place the preferred-player setting is resolved. Four widgets used
	// to carry their own copy of this block and had already drifted apart.
	readonly property string preferredPlayerId: MprisSelection.normalizePreferredPlayer(
		Config.options.bar.media.preferredPlayer);
	readonly property bool preferenceApplies: MprisSelection.preferenceMatches(players, preferredPlayerId).length > 0;
	readonly property var meaningfulPlayers: MprisSelection.meaningfulPlayers(players, preferredPlayerId);
	readonly property var playerOptions: MprisSelection.playerOptions(players, preferredPlayerId);

	function hasUsableMetadata(player) {
		return MprisSelection.hasUsableMetadata(player);
	}

	function preferredPlayer(candidates) {
		return MprisSelection.preferredPlayer(candidates);
	}

	function honoursPreference(player) {
		return !preferenceApplies || MprisSelection.matchesPreference(player, preferredPlayerId);
	}

	function reconcileTrackedPlayer(preferred) {
		if (!honoursPreference(trackedPlayer)) {
			trackedPlayer = MprisSelection.selectPlayer(players, preferredPlayerId);
			syncActivePlayer();
			return;
		}
		if (preferred && players.includes(preferred) && preferred.isPlaying && honoursPreference(preferred)) {
			trackedPlayer = preferred;
			syncActivePlayer();
			return;
		}
		if (!trackedPlayer || !players.includes(trackedPlayer)
				|| (!trackedPlayer.isPlaying && players.some(player => player.isPlaying))) {
			trackedPlayer = MprisSelection.selectPlayer(players, preferredPlayerId);
		}
		syncActivePlayer();
	}

	function syncActivePlayer() {
		const nextPlayer = trackedPlayer && players.includes(trackedPlayer) && honoursPreference(trackedPlayer)
			? trackedPlayer : MprisSelection.selectPlayer(players, preferredPlayerId);
		if (activePlayer !== nextPlayer)
			activePlayer = nextPlayer;
	}

	onPreferredPlayerIdChanged: reconcileTrackedPlayer(null)

	onPlayersChanged: reconcileTrackedPlayer(null)
	onTrackedPlayerChanged: syncActivePlayer()

	// Original stuff from fox below
	Instantiator {
		model: Mpris.players;

		Connections {
			required property MprisPlayer modelData;
			target: modelData;

			Component.onCompleted: {
				root.reconcileTrackedPlayer(modelData.isPlaying ? modelData : null);
			}

			Component.onDestruction: {
				Qt.callLater(() => root.reconcileTrackedPlayer(null));
			}

			function onPlaybackStateChanged() {
				if (modelData.isPlaying)
					root.reconcileTrackedPlayer(modelData);
				else if (root.trackedPlayer === modelData)
					root.reconcileTrackedPlayer(null);
			}

			function onTrackTitleChanged() {
				if (modelData.isPlaying)
					root.reconcileTrackedPlayer(modelData);
			}
		}
	}

	Connections {
		target: activePlayer

		function onPostTrackChanged() {
			root.updateTrack();
		}

		function onTrackArtUrlChanged() {
			// console.log("arturl:", activePlayer.trackArtUrl)
			// root.updateTrack();
			if (root.activePlayer.uniqueId == root.activeTrack.uniqueId && root.activePlayer.trackArtUrl != root.activeTrack.artUrl) {
				// cantata likes to send cover updates *BEFORE* updating the track info.
				// as such, art url changes shouldn't be able to break the reverse animation
				const r = root.__reverse;
				root.updateTrack();
				root.__reverse = r;

			}
		}
	}

	onActivePlayerChanged: this.updateTrack();

	function updateTrack() {
		//console.log(`update: ${this.activePlayer?.trackTitle ?? ""} : ${this.activePlayer?.trackArtists}`)
		this.activeTrack = {
			uniqueId: this.activePlayer?.uniqueId ?? 0,
			artUrl: MediaArt.resolve(this.activePlayer?.trackArtUrl ?? "", this.activePlayer?.metadata),
			title: this.activePlayer?.trackTitle || Translation.tr("Unknown Title"),
			artist: this.activePlayer?.trackArtist || Translation.tr("Unknown Artist"),
			album: this.activePlayer?.trackAlbum || Translation.tr("Unknown Album"),
		};

		this.trackChanged(__reverse);
		this.__reverse = false;
	}

	property bool isPlaying: this.activePlayer && this.activePlayer.isPlaying;
	property bool canTogglePlaying: this.activePlayer?.canTogglePlaying ?? false;
	function togglePlaying() {
		if (this.canTogglePlaying) this.activePlayer.togglePlaying();
	}

	property bool canGoPrevious: this.activePlayer?.canGoPrevious ?? false;
	function previous() {
		if (this.canGoPrevious) {
			this.__reverse = true;
			this.activePlayer.previous();
		}
	}

	property bool canGoNext: this.activePlayer?.canGoNext ?? false;
	function next() {
		if (this.canGoNext) {
			this.__reverse = false;
			this.activePlayer.next();
		}
	}

	property bool canChangeVolume: this.activePlayer && this.activePlayer.volumeSupported && this.activePlayer.canControl;

	property bool loopSupported: this.activePlayer && this.activePlayer.loopSupported && this.activePlayer.canControl;
	property var loopState: this.activePlayer?.loopState ?? MprisLoopState.None;
	function setLoopState(loopState: var) {
		if (this.loopSupported) {
			this.activePlayer.loopState = loopState;
		}
	}

	property bool shuffleSupported: this.activePlayer && this.activePlayer.shuffleSupported && this.activePlayer.canControl;
	property bool hasShuffle: this.activePlayer?.shuffle ?? false;
	function setShuffle(shuffle: bool) {
		if (this.shuffleSupported) {
			this.activePlayer.shuffle = shuffle;
		}
	}

	function setActivePlayer(player: MprisPlayer) {
		const targetPlayer = player ?? Mpris.players[0];
		console.log(`[Mpris] Active player ${targetPlayer} << ${activePlayer}`)

		if (targetPlayer && this.activePlayer) {
			this.__reverse = Mpris.players.indexOf(targetPlayer) < Mpris.players.indexOf(this.activePlayer);
		} else {
			// always animate forward if going to null
			this.__reverse = false;
		}

		this.trackedPlayer = targetPlayer;
		this.syncActivePlayer();
	}

	IpcHandler {
		target: "mpris"

		function debugState(): string {
			return JSON.stringify({
				rawCount: Mpris.players.values.length,
				filteredCount: root.players.length,
				tracked: root.trackedPlayer?.dbusName ?? "",
				active: root.activePlayer?.dbusName ?? "",
				trackTitle: root.trackTitle,
				trackArtist: root.trackArtist,
				position: root.position,
				length: root.length,
				players: Mpris.players.values.map(player => ({
					dbusName: player.dbusName ?? "",
					identity: player.identity ?? "",
					isPlaying: player.isPlaying ?? false,
					playbackState: String(player.playbackState ?? ""),
					title: player.trackTitle ?? "",
					artist: player.trackArtist ?? ""
				}))
			});
		}

		function pauseAll(): void {
			for (const player of Mpris.players.values) {
				if (player.canPause) player.pause();
			}
		}

		function playPause(): void { root.togglePlaying(); }
		function previous(): void { root.previous(); }
		function next(): void { root.next(); }
	}
}
